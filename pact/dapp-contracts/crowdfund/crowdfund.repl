(begin-tx)

;;Load Namespace Contract
(env-data
 { 'ns-admin-keyset: ["admin"]
 , 'ns-operate-keyset: ["operate"]
 , 'ns-genesis-keyset: { "keys": [], "pred": "="} })
(load "../../dapp-contracts/namespaces/ns.pact")

;;Load Coin Contract
(load "../../dapp-contracts/coin-contract/fungible-v2.pact")
(load "../../dapp-contracts/coin-contract/coin.pact")
(create-table coin.coin-table)

(commit-tx)

(begin-tx)
(env-keys ["admin" "kate" "bob" "crowd" "s"])
(env-data { "admin-keyset": ["admin"],"crowd-keyset": ["crowd"], "kate-keyset": ["kate"], "bob-keyset": ["bob"] })
(load "crowdfund-pacts.pact")
(commit-tx)
(typecheck "user.crowdfund-campaign")
(verify "user.crowdfund-campaign")

;;Set block time to (time "2019-05-08T17:03:20Z")
(begin-tx)
(env-chain-data {"block-time": (time "2019-05-08T17:03:20Z")})
(use user.crowdfund-campaign)
(use coin)

(test-capability (COINBASE))
(coinbase "kate-acct" (read-keyset "kate-keyset") 500.0)
(coinbase "bob-acct" (read-keyset "bob-keyset") 500.0)

(create-account "crowd-acct" (read-keyset "crowd-keyset"))
(create-campaign "project1" "DESCRIPTION" "crowd-acct" 800.0 (time "2019-08-21T12:00:00Z") (time "2019-08-26T12:00:00Z"))

(read-campaigns)
(commit-tx)

;;Initiate Funding
(begin-tx)
(use user.crowdfund-campaign)
(use coin)

(env-chain-data {"block-time": (time "2019-08-22T12:00:00Z")})
(env-sigs [{
  'key: "kate",
  'caps: [
    (coin.TRANSFER "kate-acct" "escrow-0" 400.0)
    (ACCT_GUARD "kate-acct")]}])

;;Execute step 0 of kate-acct's "fund-campaign"
(fund-campaign "kate-acct" "project1" 400.0 "escrow-0")

(expect "100.0" 100.0 (coin.get-balance 'kate-acct))
(expect "400.0" 400.0 (coin.get-balance 'escrow-0))
(commit-tx)

(begin-tx)
(use user.crowdfund-campaign)

(env-hash (hash "hello"))

(env-sigs [{
  'key: "bob",
  'caps: [
    (coin.TRANSFER "bob-acct" "escrow-1" 500.0)
    (ACCT_GUARD "bob-acct")]}])

;;Execute step 0 of bob-acct's "fund-campaign"
(fund-campaign "bob-acct" "project1" 500.0 "escrow-1")

(expect "0.0" 0.0 (coin.get-balance 'bob-acct))
(expect "500.0" 500.0 (coin.get-balance 'escrow-1))

(commit-tx)

;;First Scenario - refund executed by signing with kate and bob's keyset
(begin-tx)
(env-sigs [
  {
  'key: "escrow-0",
  'caps: [
    (coin.TRANSFER "escrow-0" "kate-acct" 400.0)]}
  {
    'key:  "kate",
    'caps: []}
  {
  'key: "escrow-1",
  'caps: [
    (coin.TRANSFER "escrow-1" "bob-acct" 500.0)]}
  {
    'key:  "bob",
    'caps: []}
    ])
(use user.crowdfund-campaign)

;;Kate's fund 400.0 is sent to escrow-0 , Bob's fund 500.0 is sent to escrow-1
(expect "100.0" 100.0 (coin.get-balance 'kate-acct))
(expect "0.0" 0.0 (coin.get-balance 'bob-acct))
(expect "400.0" 400.0 (coin.get-balance 'escrow-0))
(expect "500.0" 500.0 (coin.get-balance 'escrow-1))

;;Rollback kate's fund-campaign
(expect "Kate's refund executed"
  "Write succeeded"
  (continue-pact 0 true "DldRwCblQ7Loqy6wYJnaodHl30d3j3eH-qtFzfEv46g"))

;;Rollback Bob's fund-campaign
(expect "Bob's refund executed"
  "Write succeeded"
  (continue-pact 0 true "Mk3PAn3UowqTLEQfNlol6GsXPe-kuOWJSCU0cbgbcs8"))

;;Kate and Bob's funds are refunded
(expect "0.0" 0.0 (coin.get-balance 'escrow-0))
(expect "0.0" 0.0 (coin.get-balance 'escrow-1))
(expect "500.0" 500.0 (coin.get-balance 'kate-acct))
(expect "500.0" 500.0 (coin.get-balance 'bob-acct))

(rollback-tx)

;;Second Scenario - campaign canceled by the campaign owner, so canacel is executed

(begin-tx)
(use user.crowdfund-campaign)
(env-chain-data {"block-time": (time "2019-08-24T12:00:00Z")})
(env-sigs [
  {
  'key: "crowd",
  'caps: [
    (CAMPAIGN_GUARD 'project1)]}
  {
  'key: "escrow-0",
  'caps: [
    (coin.TRANSFER "escrow-0" "kate-acct" 400.0)]}
  {
  'key: "escrow-1",
  'caps: [
    (coin.TRANSFER "escrow-1" "bob-acct" 500.0)]}
  ])

(cancel-campaign 'project1)
;;Rollback kate's fund-campaign
(expect "Kate's refund executed"
  "Write succeeded"
  (continue-pact 0 true "DldRwCblQ7Loqy6wYJnaodHl30d3j3eH-qtFzfEv46g"))

;;Rollback Bob's fund-campaign
(expect "Bob's refund executed"
  "Write succeeded"
  (continue-pact 0 true "Mk3PAn3UowqTLEQfNlol6GsXPe-kuOWJSCU0cbgbcs8"))

(rollback-tx)

;;Third Scenario - campaign succeeds
(begin-tx)
(env-chain-data {"block-time": (time "2019-08-22T12:00:00Z")})

(env-sigs [
  {
  'key: "escrow-0",
  'caps: [
    (coin.TRANSFER "escrow-0" "crowd-acct" 400.0)]}
  {
  'key: "escrow-1",
  'caps: [
    (coin.TRANSFER "escrow-1" "crowd-acct" 500.0)]}
  ])
(use user.crowdfund-campaign)
(env-hash (hash "hellddo"))
(env-chain-data {"block-time": (time "2019-08-27T12:00:00Z")})

(succeed-campaign 'project1)

;;Rollback Fails
(expect-failure "Can't rollback once campaign succeeds"
  (continue-pact 0 true "DldRwCblQ7Loqy6wYJnaodHl30d3j3eH-qtFzfEv46g"))

(expect "Kate's Fund transferred to campaign owner"
  "Write succeeded"
  (continue-pact 1 false "DldRwCblQ7Loqy6wYJnaodHl30d3j3eH-qtFzfEv46g"))

(expect "Bob's Fund transferred to campaign owner"
  "Write succeeded"
  (continue-pact 1 false "Mk3PAn3UowqTLEQfNlol6GsXPe-kuOWJSCU0cbgbcs8"))

;;Funds in all accounts
(expect "money went through after campaign success" 900.0 (coin.get-balance 'crowd-acct))
(expect "0.0" 0.0 (coin.get-balance 'escrow-0))
(expect "0.0" 0.0 (coin.get-balance 'escrow-1))
(expect "100.0" 100.0 (coin.get-balance 'kate-acct))
(expect "0.0" 0.0 (coin.get-balance 'bob-acct))

(rollback-tx)
